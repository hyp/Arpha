import spec

describe "A 'var' expression" {
	it "should declare a mutable variable in the current block" {
		var i int32 = 1
		i should be 1
		i = 4
		i should be 4
	}
	it "should allow the variable's type to be inferred" {
		var i = false
		i should be false
	}
	it "should allow multiple variable declarations which are separated by comma" {
		var x,y = 5,6
		x should be 5
		y should be 6
	}
	it "should shadow a variable in an outer block" {
		var i = true
		{
			var i = false
			i should be false
		}
		i should be true
	}
	it "should return the variable's value when only one variable is declared" {
		(var x = false) should be false
	}
}

describe "An immutable variable expanded at compile time" {
	it "should be defined with 'def'" {
		def pi = 3.14
		pi should be 3.14
		def life arpha.literal.integer = 42
		life should be 42
	}
	it "should return a constant with a literal type when referenced" {
		def foo = 2.71
		foo should be 2.71
	}
}
